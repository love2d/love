AC_INIT([love], [HEAD])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_AUX_DIR([platform/unix])
AC_CONFIG_MACRO_DIR([platform/unix/m4])
AC_CONFIG_SRCDIR([src/love.cpp])
AM_INIT_AUTOMAKE([foreign -Wall foreign tar-ustar])
AC_PREFIX_DEFAULT([/usr])
AC_PROG_LIBTOOL
AC_PROG_CC
AC_PROG_CXX
AC_SEARCH_LIBS([sqrt], [m], [], AC_MSG_ERROR([Can't LÖVE without C math library]))
AC_SEARCH_LIBS([SDL_Init], [SDL], [], AC_MSG_ERROR([Can't LÖVE without SDL]))
AC_SEARCH_LIBS([glLoadIdentity], [GL], [], AC_MSG_ERROR([Can't LÖVE without OpenGL]))
#AC_SEARCH_LIBS([gluOrtho2D], [GLU], [], AC_MSG_ERROR([Can't LÖVE without OpenGL Utility Library]))
AC_SEARCH_LIBS([alSourcePlay], [openal], [], AC_MSG_ERROR([Can't LÖVE without OpenAL]))
lua=lua
AC_ARG_WITH([luajit],
	    [AS_HELP_STRING([--with-luajit], [Use LuaJIT instead of lua and llvm-lua])],
	    [lua=luajit],
	    [])
AC_ARG_WITH([llvm-lua],
	    [AS_HELP_STRING([--with-llvm-lua], [Use llvm-lua instead of lua and LuaJIT])],
	    [lua=llvm-lua],
	    [])

AS_IF([test "$lua" == "lua"],
	AC_SEARCH_LIBS(
		[lua_pcall],
		[lua lua5.1],
		if test "$ac_cv_search_lua_pcall" = "-llua5.1"; then
			AC_SUBST([INCLUDE_LUA], [-I/usr/include/lua5.1])
		fi,
		AC_MSG_ERROR([Can't LÖVE without Lua])
	)
      )
AS_IF([test "$lua" == "luajit"],
	AC_SEARCH_LIBS(
		[lua_pcall],
		[luajit luajit-5.1],
		AC_SUBST([INCLUDE_LUA], [-I/usr/include/luajit-2.0]),
		AC_MSG_ERROR([Can't LÖVE without LuaJIT])
	)
      )
AS_IF([test "$lua" == "llvm-lua"],
	AC_SEARCH_LIBS(
		[lua_pcall],
		[llvm-lua],
		[],
		AC_MSG_ERROR([Can't LÖVE without llvm-lua])
	)
      )

AC_SEARCH_LIBS([ilInit], [IL], [], AC_MSG_ERROR([Can't LÖVE without DevIL]))
AC_SEARCH_LIBS([FT_Load_Glyph], [freetype], [], AC_MSG_ERROR([Can't LÖVE without FreeType]))
AC_SEARCH_LIBS([PHYSFS_init], [physfs], [], AC_MSG_ERROR([Can't LÖVE without PhysicsFS]))
AC_SEARCH_LIBS([ModPlug_Load], [modplug], [], AC_MSG_ERROR([Can't LÖVE without ModPlug]))
AC_SEARCH_LIBS([mpg123_open_feed], [mpg123], [], AC_MSG_ERROR([Can't LÖVE without Mpg123]))
AC_SEARCH_LIBS([mpg123_seek_64], [mpg123], AC_SUBST([FILE_OFFSET],[-D_FILE_OFFSET_BITS=64]), AC_SUBST([FILE_OFFSET],[]))
AC_SEARCH_LIBS([ov_open], [vorbisfile], [], AC_MSG_ERROR([Can't LÖVE without VorbisFile]))
AC_ARG_ENABLE([headless],
	      [  --enable-headless Build with less SDL],
	      AC_DEFINE([LOVE_HEADLESS], [], [Build with less SDL]), [])
AC_CONFIG_FILES([
	Makefile
	src/Makefile
])
AC_OUTPUT
